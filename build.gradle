buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url "https://dev.saxonica.com/maven"
    }
  }

  dependencies {
    classpath group: "com.saxonica", name: "Saxon-EE", version: "10.3"
  }
}

plugins {
  id "java"
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "https://dev.saxonica.com/maven"
  }
}

configurations.all {
  resolutionStrategy {
    force 'xml-apis:xml-apis:1.4.01'
  }
}

configurations {
  saxon.extendsFrom(implementation)
}

// Set saxonLicenseDir in gradle.properties, or from the
// command line if you have a license in some other place.
if (!hasProperty("saxonLicenseDir")) {
  ext.saxonLicenseDir=System.getenv('HOME') + "/java"
}

dependencies {
  implementation (
    [group: "com.saxonica", name: "Saxon-EE", version: "10.3"]
  )
  saxon (
    files(saxonLicenseDir)
  )
}

defaultTasks 'compileXslt'

if (file(saxonLicenseDir + "/saxon-license.lic").exists()) {
  // We can compile the stylesheet
  task compileXslt(type: JavaExec) {
    inputs.file "stylesheet.xsl"
    outputs.file "stylesheet.sef.json"
    classpath = configurations.saxon
    main = "com.saxonica.Transform"
    args "-t",
      "-xsl:stylesheet.xsl",
      "-export:stylesheet.sef.json",
      "-target:JS", "-nogo", "-relocate:on", "-ns:##html5"
  }
} else {
  throw new GradleException("Failed to find Saxon license.")
}
